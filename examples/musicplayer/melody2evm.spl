#!/usr/bin/env splrun

var timestep = 6;
var timescale = 6;

var tones, timings;
var melody;

var line;
var ontime, offtime, tone;
var min_tone, min_on, min_off;
var max_tone, max_on, max_off;

function min(a, b)
{
	if (not defined a)
		return b;
	if (not defined b)
		return a;
	return a < b ? a : b;
}

function max(a, b)
{
	if (not defined a)
		return b;
	if (not defined b)
		return a;
	return a > b ? a : b;
}

for (line = read(); defined line; line = read())
{
	var f = line =~ /, */Sg;
	if (defined offtime) {
		var onduration = round((offtime - ontime) ./ timestep);
		var offduration = round((+f[1] - offtime) ./ timestep);
		// write("$tone $onduration $offduration\n");
		if (not declared tones["$tone"])
			tones["$tone"] = elementsof tones;
		if (not declared timings["$onduration $offduration"])
			timings["$onduration $offduration"] = elementsof timings;
		push melody, (tones["$tone"] * 32) + timings["$onduration $offduration"];
		min_on = min(min_on, onduration);
		max_on = max(max_on, onduration);
		min_off = min(min_off, offduration);
		max_off = max(max_off, offduration);
		offtime = undef;
	}
	if (f[2] ~== "Note_on_c") {
		ontime = +f[1];
		offtime = undef;
		tone = +f[4];
		min_tone = min(min_tone, tone);
		max_tone = max(max_tone, tone);
	}
	if (f[2] ~== "Note_off_c") {
		offtime = +f[1];
	}
}

write("array16 melody_freq[${elementsof(tones)}] = {");
foreach t (tones) {
	var freq = round(440 * 2**((t-69)./12));
	write("$freq${ $] ? "" : "," }");
}
write("};\n");

write("array8u melody_timing_on[${elementsof(timings)}] = {");
foreach t (timings) {
	write("${ t =~ s/ .*//R }${ $] ? "" : "," }");
}
write("};\n");

write("array8u melody_timing_off[${elementsof(timings)}] = {");
foreach t (timings) {
	write("${ t =~ s/.* //R }${ $] ? "" : "," }");
}
write("};\n");

write("array8u melody_data[${elementsof(melody)+1}] = {");
foreach[] note (melody) {
	write("$note,");
}
write("255};\n");

// write("min_tone = $min_tone, max_tone = $max_tone, min_on = $min_on, max_on = $max_on, "
// 		"min_off = $min_off, max_off = $max_off\n");

write(<:>
	: function main()
	: {
	:	local i, tone, timeidx;
	:	for (i=0;; i++) {
	:		tone = melody_data[i];
	:		if (tone == 0xff)
	:			break;
	:		timeidx = melody_data[i] & 31;
	:		// $$uf0(i, tone, timeidx);
	:		$$uf1(melody_freq[tone >> 5],
	:			$timescale * melody_timing_on[timeidx],
	:			$timescale * melody_timing_off[timeidx]);
	:	}
	: }
</>);

